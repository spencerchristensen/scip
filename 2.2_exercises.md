# Exercises

```elixir
Mix.install([])
```

## Exercise 2.17

> Define a procedure `last-pair` that returns the list that contains only the last element of a given (nonempty) list:
> `(last-pair (list 23 72 149 34))`
> 
> *(34)*

<!-- livebook:{"break_markdown":true} -->

Lambda solution:

```elixir
last_pair = fn list, f ->
  case list do
    [head | []] -> [head]
    [_ | tail] -> f.(tail, f)
  end
end

IO.inspect(last_pair.([23, 72, 149, 34], last_pair), charlists: :as_lists)
```

Module solution:

```elixir
defmodule LastPair do
  def last_pair([head | []]) do
    [head]
  end

  def last_pair([_ | tail]) do
    last_pair(tail)
  end
end

IO.inspect(LastPair.last_pair([23, 72, 149, 34]), charlists: :as_lists)
```

## Exercise 2.18

> Define a procedure `reverse` that takes a list as argument and returns a list of the same elements in reverse order.

<!-- livebook:{"break_markdown":true} -->

One thing to note up front. Since the order of the list is different than the original list, I will need to add an accumulator to track my new list so that it can be used in list operations. Otherwise I would need to append lists which is less ideal.

Lambda solution:

```elixir
reverse = fn list, f ->
  case list do
    [head | []] -> [head]
    # appending!
    [head | tail] -> f.(tail, f) ++ [head]
  end
end

reverse.([1, 2, 3, 4, 5], reverse)
```

```elixir
reverse_acc = fn list, acc, f ->
  case list do
    [] -> acc
    # no appending!
    [head | tail] -> f.(tail, [head | acc], f)
  end
end

reverse_acc.([1, 2, 3, 4, 5], [], reverse_acc)
```

Module solution:

```elixir
defmodule ReverseWithAppending do
  def reverse([]), do: []

  def reverse([head | tail]) do
    reverse(tail) ++ [head]
  end
end

ReverseWithAppending.reverse([1, 2, 3, 4, 5])
```

The above works, but results in the following process:

```
Initial list: [1, 2, 3, 4, 5]

Step 1: reverse([2, 3, 4, 5]) ++ [1]
Step 2: (reverse([3, 4, 5]) ++ [2]) ++ [1]
Step 3: ((reverse([4, 5]) ++ [3]) ++ [2]) ++ [1]
Step 4: (((reverse([5]) ++ [4]) ++ [3]) ++ [2]) ++ [1]
Step 5: ((((reverse([]) ++ [5]) ++ [4]) ++ [3]) ++ [2]) ++ [1]
Step 6: ((([5] ++ [4]) ++ [3]) ++ [2]) ++ [1]
Step 7: (([5, 4] ++ [3]) ++ [2]) ++ [1]
Step 8: ([5, 4, 3] ++ [2]) ++ [1]
Step 9: [5, 4, 3, 2] ++ [1]
Step 10: [5, 4, 3, 2, 1]

Final list: [5, 4, 3, 2, 1]
```

Let's add an accumulator to keep this as an iterative & TCO'd process:

```elixir
defmodule ReverseWithAccumulator do
  def reverse(list), do: do_reverse(list, [])

  defp do_reverse([], acc), do: acc

  defp do_reverse([head | tail], acc) do
    do_reverse(tail, [head | acc])
  end
end

ReverseWithAccumulator.reverse([1, 2, 3, 4, 5])
```

```
Initial list: [1, 2, 3, 4, 5]
Accumulator: []

Step 1: do_reverse([2, 3, 4, 5], [1])
Step 2: do_reverse([3, 4, 5], [2, 1])
Step 3: do_reverse([4, 5], [3, 2, 1])
Step 4: do_reverse([5], [4, 3, 2, 1])
Step 5: do_reverse([], [5, 4, 3, 2, 1])

Final list: [5, 4, 3, 2, 1]
```

<!-- livebook:{"break_markdown":true} -->

## Exercise 2.20

> Use dot notation to write a procedure `same-parity` that takes one or more integers and returns a list of all the arguments that have the same even-odd parity as the first argument.

<!-- livebook:{"break_markdown":true} -->

Elixir does not have variadic functions, making this a little different.

Lambda solution:

```elixir
require Integer

same_parity = fn [head | tail] ->
  parity_fn = if Integer.is_even(head), do: &Integer.is_even/1, else: &Integer.is_odd/1
  [head | Enum.filter(tail, parity_fn)]
end

same_parity.([1, 2, 3, 4, 5, 6, 7])
```

Module solution:

```elixir
defmodule Parity do
  def same_parity([head | tail]) when Integer.is_even(head) do
    [head | Enum.filter(tail, &Integer.is_even/1)]
  end

  def same_parity([head | tail]) when Integer.is_odd(head) do
    [head | Enum.filter(tail, &Integer.is_odd/1)]
  end
end

Parity.same_parity([1, 2, 3, 4, 5, 6, 7])
```

## Exercise 2.21

> The procedure square-list takes a list of numbers as argument and returns a list of the squares of those numbers.
> 
> ```scheme
> (square-list (list 1 2 3 4))
> (1 4 9 16)
> ```
> 
> Here are two different definitions of square-list. Complete both of them by filling in the missing expressions:
> 
> ```scheme
> (define (square-list items) (if (null? items) nil (cons ⟨??⟩ ⟨??⟩)))
> ```
> 
> &
> 
> ```scheme
> (define (square-list items) (map ⟨??⟩ ⟨??⟩))
> ```

```elixir
defmodule SquareList do
  def square_list([]), do: []
  def square_list([head | tail]), do: [head * head | square_list(tail)]
end

SquareList.square_list([2, 3, 4, 5])
```

```elixir
square_list = fn items ->
  Enum.map(items, fn x -> x * x end)
end

square_list.([2, 3, 4, 5])
```

## Exercise 2.23

> The procedure `for-each` is similar to `map`. It takes as arguments a procedure and a list of elements. However, rather than forming a list of the results, `for-each` just applies the procedure to each of the elements in turn, from left to right. The values returns by applying the procedure to the elements are not used at all - `for-each` is used with procedures that perform an action, such as printing. The value returned by the call to `for-each` can be something arbitary such as true. Give an implementation for `for-each`.

```elixir
defmodule Foreach do
  def foreach(_proc, []), do: :ok
  def foreach(_proc, nil), do: {:error, "Input list cannot be nil"}

  def foreach(proc, [head | tail]) do
    proc.(head)
    foreach(proc, tail)
  end
end

Foreach.foreach(&IO.inspect/1, [1, ~c"hello", {5, 6}])
```

## Exercise 2.27

> Modify your reverse procedure of exercise 2.18 to produce a deep-reverse procedure that takes a list as an argument and returns as its value the list with its elements reversed and with all sublists deep-reversed as well.
> 
> ```scheme
> (define x (list (list 1 2) (list 3 4)))
> ; ((1 2) (3 4))
> (deep-reverse x)
> ; ((4 3) (2 1))
> ```

<!-- livebook:{"break_markdown":true} -->

Ok, let's do an initial pass that uses concatenation to create the list:

```elixir
list_to_deep_reverse = [[1, 2], [3, 4]]
```

```elixir
defmodule DeepReverseConcat do
  def deep_reverse([]), do: []

  def deep_reverse([head | tail]) when is_list(head) do
    deep_reverse(tail) ++ [deep_reverse(head)]
  end

  def deep_reverse([head | tail]) do
    deep_reverse(tail) ++ [head]
  end
end

DeepReverseConcat.deep_reverse(list_to_deep_reverse)
```

That works, but is inefficient as list concatenation requires the traversal of the entire list and is thus a $O(n)$ operation.

Let's refactor it to use an accumulator:

```elixir
defmodule DeepReverseWithAccumulator do
  def deep_reverse(list), do: do_reverse(list, [])

  defp do_reverse([], acc), do: acc

  defp do_reverse([head | tail], acc) when is_list(head) do
    # oops!
    do_reverse(tail, [do_reverse(head, acc) | acc])
  end

  defp do_reverse([head | tail], acc) do
    do_reverse(tail, [head | acc])
  end
end

DeepReverseWithAccumulator.deep_reverse(list_to_deep_reverse)
```

Well, the leaves are being reversed but the branches are being duplicated, oops! Ah, I see that my error was to pass the existing accumulator to the recursive function. This does not make any sense even though it, well, "felt recursive". If I determine that a given node is a branch, then to "recurse" in this instance is to go down the branch, treat it as it's own list, and *leave the invoking function to worry about `cons`-ing the result*.

```elixir
defmodule SmellyDeepReverseWithAccumulator do
  def deep_reverse(list), do: do_reverse(list, [])

  defp do_reverse([], acc), do: acc

  defp do_reverse([head | tail], acc) when is_list(head) do
    # this works, but smells
    do_reverse(tail, [do_reverse(head, []) | acc])
  end

  defp do_reverse([head | tail], acc) do
    do_reverse(tail, [head | acc])
  end
end

SmellyDeepReverseWithAccumulator.deep_reverse(list_to_deep_reverse)
```

That works, though the `do_reverse when is_list(head)` on line 6 feels a little off. It seems redundant to have to be passing a brand new accumulator on line 7.

We can solve this by realizing that if `head` is a list, then we can use the original public method as that is in fact built to know how to handle a list by itself invoke the relevant private functions to perform the reversal, and manage the accumulator.

```elixir
defmodule BetterDeepReverse do
  def deep_reverse(list), do: do_deep_reverse(list, [])

  defp do_deep_reverse([], acc), do: acc

  defp do_deep_reverse([head | tail], acc) when is_list(head) do
    # better!
    do_deep_reverse(tail, [deep_reverse(head) | acc])
  end

  defp do_deep_reverse([head | tail], acc) do
    do_deep_reverse(tail, [head | acc])
  end
end

BetterDeepReverse.deep_reverse(list_to_deep_reverse)
```

This is a much cleaner implementation. Not only is an accumulator being used to enable TCO and keep it as an iteratively-shaped process, but the private methods are using the public method just as anybody else would. By utitlizing `deep_reverse` instead of `do_deep_reverse` on line 7 we decrease coupling and keep interfaces and utilization much cleaner internally. The module's interface has been made recursive, not just its internals.

<!-- livebook:{"break_markdown":true} -->

## Exercise 2.30

<!-- livebook:{"break_markdown":true} -->

> Define a procedure `square-tree` analogous to the `square-list` procedure of exercise 2.21.
> 
> ```scheme
> (square-tree 
>   (list 1
>         (list 2 (list 3 4) 5) 
>         (list 6 7))) 
> ; (1 ( 4 (9 16) 25) (36 49))
> ```

```elixir
list_to_square = [1, [2, [3, 4], 5], [6, 7]]

defmodule SquareTree do
  def square_tree(list) do
    do_square_tree(list, [])
  end

  defp do_square_tree([], acc), do: Enum.reverse(acc)

  defp do_square_tree([head | tail], acc) when is_list(head) do
    do_square_tree(tail, [square_tree(head) | acc])
  end

  defp do_square_tree([head | tail], acc) do
    do_square_tree(tail, [head * head | acc])
  end
end

IO.inspect(SquareTree.square_tree(list_to_square), charlists: :as_lists)
```

The above uses TCO recursion. Let's refactor to simply utilize `Enum.map`:

```elixir
defmodule SquareTreeEnumMap do
  def square_tree(list) do
    Enum.map(list, fn
      x when is_list(x) -> square_tree(x)
      x -> x * x
    end)
  end
end

IO.inspect(SquareTreeEnumMap.square_tree(list_to_square), charlists: :as_lists)
```

## Exercise 2.31

<!-- livebook:{"break_markdown":true} -->

> Abstract your answer to exercise 2.30 to produce a procedure `tree-map` with the property that `square-tree` could be defined as:
> 
> ```scheme
> (define (square-tree tree) (tree-map square tree))
> ```

```elixir
defmodule TreeMap do
  def tree_map(f, tree) do
    Enum.map(tree, fn
      x when is_list(x) -> tree_map(f, x)
      x -> f.(x)
    end)
  end
end

defmodule HigherOrderSquareTree do
  def square_tree(tree) do
    TreeMap.tree_map(fn x -> x * x end, tree)
  end
end

IO.inspect(HigherOrderSquareTree.square_tree(list_to_square), charlists: :as_lists)
```
